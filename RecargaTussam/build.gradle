// Gradle data
buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.1'
        // the latest version of the android-apt plugin
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'io.fabric.tools:gradle:1.+'

    }
}

// Gradle plugins here
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'

// Maven dependencies repositories in here
repositories {
    mavenCentral()
    maven { url 'http://dev.mobivery.com/artifactory/repo' }
    maven { url 'https://maven.fabric.io/public' }

}

apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.outputs[0].processResources.manifestFile
    }
}

// Dependencies versions (the ones that usually change)
ext.androidAnnotationsVersion = '3.0.1'
ext.mobiveryLibAndroidVersion = '1.0.4-SNAPSHOT'

// Insert your dependencies in here
dependencies {
    compile "com.android.support:appcompat-v7:+"
    compile 'com.android.support:support-v4:+'
    apt "org.androidannotations:androidannotations:${androidAnnotationsVersion}"
    compile "org.androidannotations:androidannotations-api:${androidAnnotationsVersion}"
    compile "com.mobivery.android:library:${mobiveryLibAndroidVersion}@aar"
    compile 'org.jsoup:jsoup:1.7.3'
    compile "com.android.support:appcompat-v7:21.0.+"
    compile 'com.google.android.gms:play-services-wearable:6.5.87'
    wearApp project(':bonobuswear')
    compile('com.crashlytics.sdk.android:crashlytics:2.2.1@aar') {
        transitive = true;
    }
    //wearApp files('/Users/claucookie/Desktop/wearable_app.apk')
}

// Android compilation information
android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        applicationId "es.claucookie.recarga"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-dev"
        }
    }

    productFlavors {
        recargatussam {
            applicationId "es.claucookie.recarga"
            versionName "2.1"
            versionCode 13
        }
        recargaaucorsa {
            applicationId "es.claucookie.recargaaucorsa"
            versionName "1.0"
            versionCode 1
        }
    }

}

// Parametrized signing code for CI and Builder Dash
if (project.hasProperty('keyStore')) {
    android.signingConfigs.release.storeFile = file(keyStore)
}
if (project.hasProperty('keyStorePassword')) {
    android.signingConfigs.release.storePassword = keyStorePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyAliasPassword')) {
    android.signingConfigs.release.keyPassword = keyAliasPassword
}
