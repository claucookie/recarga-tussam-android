// Gradle data
buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.0'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.9.+'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

// Gradle plugins here
apply plugin: 'crashlytics'
apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'



// Maven dependencies repositories in here
repositories {
    mavenCentral()
    maven { url 'http://dev.mobivery.com/artifactory/repo' }
    maven { url 'http://download.crashlytics.com/maven' }

}

// Needed by AA
apt {
    arguments {
        resourcePackageName android.productFlavors.production.applicationId
        androidManifestFile variant.processResources.manifestFile
    }

}

// Dependencies versions (the ones that usually change)
ext.androidAnnotationsVersion = '3.1'
ext.mobiveryLibAndroidVersion = '1.0.4-SNAPSHOT'
ext.universalImageLoaderVersion = '1.9.1'

// Insert your dependencies in here
dependencies {
    compile 'com.android.support:support-v4:20.+'
    apt "org.androidannotations:androidannotations:${androidAnnotationsVersion}"
    compile "org.androidannotations:androidannotations-api:${androidAnnotationsVersion}"
    compile "com.mobivery.android:library:${mobiveryLibAndroidVersion}@aar"
    compile "com.nostra13.universalimageloader:universal-image-loader:${universalImageLoaderVersion}"
    compile 'com.crashlytics.android:crashlytics:1.+'
    compile 'org.jsoup:jsoup:1.7.3'

}

// Android compilation information
android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        production {
            applicationId "es.claucookie.recarga"
        }

        staging {
            applicationId "es.claucookie.recargadev"
        }
    }

}

// Parametrized signing code for CI and Builder Dash
if (project.hasProperty('keyStore')) {
    android.signingConfigs.release.storeFile = file(keyStore)
}
if (project.hasProperty('keyStorePassword')) {
    android.signingConfigs.release.storePassword = keyStorePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyAliasPassword')) {
    android.signingConfigs.release.keyPassword = keyAliasPassword
}
